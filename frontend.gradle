apply plugin: 'com.moowork.node'

def clientRoot = file("${project.rootDir}/frontend")
def clientDist = file("${clientRoot.getAbsolutePath()}/dist")

node {
    version = '10.16.3'
    npmVersion = '6.9.0'
    distBaseUrl = 'https://nodejs.org/dist'
    // If false, it will try to use globally installed node.
    download = true
    // Set the work directory for unpacking node
    workDir = new File(clientRoot, ".gradle/nodejs")
    npmWorkDir = file(".gradle/npm")
    nodeModulesDir = clientRoot
}

task clientBuild(type: NpmTask) {
    group = 'Client'

    description = 'Builds the client'
    workingDir  = clientRoot

    args = ['run', 'build']
    inputs.files fileTree(dir: clientRoot, excludes: ["dist/**", "test/**"])
    outputs.dir(clientDist)
}

task clientArtifacts(type: Copy) {
    from clientDist
    into "build/classes/static"
}

npm_install.dependsOn npmSetup
clientBuild.dependsOn npm_install
clientArtifacts.dependsOn clientBuild

